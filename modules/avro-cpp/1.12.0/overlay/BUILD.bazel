load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_library")
load("//:gen_test_targets.bzl", "gen_test_targets")

cc_library(
    name = "avro_cpp",
    srcs = glob(
        [
            "impl/**/*.cc",
            "impl/**/*.hh",
        ],
        # This is a binary and not part of the library.
        exclude = ["impl/avrogencpp.cc"],
    ),
    hdrs = glob(["include/**/*.hh"]),
    includes = [
        "include",
        "include/avro",
    ],
    local_defines = select({
        "@platforms//os:windows": [
            "NOMINMAX",
            "BOOST_REGEX_DYN_LINK",
            "BOOST_FILESYSTEM_DYN_LINK",
            "BOOST_SYSTEM_DYN_LINK",
            "BOOST_IOSTREAMS_DYN_LINK",
            "BOOST_PROGRAM_OPTIONS_DYN_LINK",
            "BOOST_ALL_NO_LIB",
        ],
        "//conditions:default": [],
    }),
    visibility = ["//visibility:public"],
    deps = [
        "@boost.algorithm",
        "@boost.crc",
        "@boost.filesystem",
        "@boost.iostreams",
        "@boost.math",
        "@boost.program_options",
        "@boost.regex",
        "@boost.system",
        "@fmt",
    ],
)

cc_binary(
    name = "avrogencpp",
    srcs = ["impl/avrogencpp.cc"],
    visibility = ["//visibility:public"],
    deps = [
        ":avro_cpp",
        "@boost.algorithm",
        "@boost.lexical_cast",
        "@boost.program_options",
        "@boost.random",
    ],
)

gen_test_targets(
    name = "gen_avro_cpp_test_targets",
    cc_srcs = glob(
        ["test/*.cc"],
        exclude = [
            # These tests require generating header files from the JSON schema
            # files using avrogencpp.
            "test/AvrogencppTestReservedWords.cc",
            "test/AvrogencppTests.cc",
            "test/testgentest.cc",
            # This test passes but fails with a pointer error.
            "test/JsonTests.cc",
            # These look like binaries rather than unit tests as they require
            # reading from stdin.
            "test/precompile.cc",
            "test/testparser.cc",
        ],
    ),
)

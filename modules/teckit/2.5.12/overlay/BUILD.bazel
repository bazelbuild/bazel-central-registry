load("@rules_cc//cc:cc_binary.bzl", "cc_binary")
load("@rules_cc//cc:cc_library.bzl", "cc_library")

# Public headers
cc_library(
    name = "teckit_headers",
    hdrs = [
        "source/Public-headers/TECkit_Common.h",
        "source/Public-headers/TECkit_Compiler.h",
        "source/Public-headers/TECkit_Engine.h",
    ],
    includes = ["source/Public-headers"],
)

# TECkit Engine library (libTECkit)
cc_library(
    name = "TECkit",
    srcs = [
        "source/Engine.cpp",
        "source/NormalizationData.c",
    ],
    hdrs = [
        "source/Engine.h",
        "source/TECkit_Format.h",
        "source/ulong_chartraits.h",
    ],
    copts = select({
        "@rules_cc//cc/compiler:msvc-cl": [
            "/FI",
            "source/Public-headers/TECkit_Common.h",
        ],
        "//conditions:default": [
            "-include",
            "source/Public-headers/TECkit_Common.h",
            "-w",
        ],
    }),
    includes = [
        "source",
        "source/Public-headers",
    ],
    textual_hdrs = [
        "source/NormalizationData.c",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":teckit_headers",
        "@zlib",
    ],
)

alias(
    name = "teckit",
    actual = ":TECkit",
    visibility = ["//visibility:public"],
)

alias(
    name = "libteckit",
    actual = ":TECkit",
    visibility = ["//visibility:public"],
)

alias(
    name = "TECkit_Engine",
    actual = ":TECkit",
    visibility = ["//visibility:public"],
)

# TECkit Compiler library (libTECkit_Compiler)
cc_library(
    name = "TECkit_Compiler",
    srcs = [
        "source/Compiler.cpp",
        "source/UnicodeNames.cpp",
    ],
    hdrs = [
        "source/Compiler.h",
        "source/TECkit_Format.h",
        "source/ulong_chartraits.h",
    ],
    copts = select({
        "@rules_cc//cc/compiler:msvc-cl": [
        ],
        "//conditions:default": [
            "-w",
        ],
    }),
    visibility = ["//visibility:public"],
    deps = [
        ":teckit_headers",
        "@zlib",
    ],
)

alias(
    name = "teckit_compiler",
    actual = ":TECkit_Compiler",
    visibility = ["//visibility:public"],
)

# teckit_compile executable
cc_binary(
    name = "teckit_compile",
    srcs = ["source/Sample-tools/TECkit_Compile.cpp"],
    visibility = ["//visibility:public"],
    deps = [
        ":TECkit_Compiler",
    ],
)

# txtconv executable
cc_binary(
    name = "txtconv",
    srcs = ["source/Sample-tools/TxtConv.cpp"],
    visibility = ["//visibility:public"],
    deps = [
        ":TECkit",
    ],
)

# sfconv executable
cc_binary(
    name = "sfconv",
    srcs = [
        "SFconv/Debug_Prefix.h",
        "SFconv/Final_Prefix.h",
        "SFconv/SFconv.cpp",
        "SFconv/UtfCodec.cpp",
        "SFconv/UtfCodec.h",
        "SFconv/sfReader.h",
        "SFconv/ushort_chartraits.h",
    ],
    copts = select({
        "@rules_cc//cc/compiler:msvc-cl": [
        ],
        "//conditions:default": [
            "-w",
        ],
    }),
    cxxopts = select({
        "@rules_cc//cc/compiler:msvc-cl": ["/std:c++11"],
        "//conditions:default": [
            "-std=c++11",
            "-w",
        ],
    }),
    defines = [
        "XML_DTD",
        "HAVE_LIBEXPAT",
    ],
    includes = [
        "SFconv",
        "source/Public-headers",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":TECkit",
        "@libexpat",
    ],
)

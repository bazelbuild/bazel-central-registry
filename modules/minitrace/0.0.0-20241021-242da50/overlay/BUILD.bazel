load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")
load("@rules_cc//cc:defs.bzl", "cc_library", "cc_test")
load("@rules_license//rules:license.bzl", "license")

license(
    name = "license",
    license_kinds = ["@rules_license//licenses/spdx:MIT"],
    license_text = "LICENSE",
)

# Set this to True enable minitrace macros by adding the following flag to your .bazelrc or passing
# to the bazel commandline: --@minitrace//:mtr_enable
bool_flag(
    name = "mtr_enable",
    build_setting_default = False,
)

config_setting(
    name = "mtr_enable_setting",
    flag_values = {":mtr_enable": "True"},
)

cc_library(
    name = "minitrace",
    srcs = ["minitrace.c"],
    hdrs = ["minitrace.h"],
    defines = select({
        ":mtr_enable_setting": ["MTR_ENABLED"],
        "//conditions:default": [],
    }),
    includes = ["."],
    visibility = ["//visibility:public"],
)

cc_test(
    name = "minitrace_test",
    size = "small",
    srcs = ["minitrace_test.cpp"],
    defines = ["MTR_ENABLED"],
    deps = [":minitrace"],
)

cc_test(
    name = "minitrace_test_mt",
    size = "small",
    srcs = ["minitrace_test_mt.cpp"],
    defines = ["MTR_ENABLED"],
    deps = [":minitrace"],
)

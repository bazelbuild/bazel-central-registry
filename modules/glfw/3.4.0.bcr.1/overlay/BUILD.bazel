load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("@rules_cc//cc:objc_library.bzl", "objc_library")

cc_library(
    name = "glfw-headers",
    hdrs = [
        "include/GLFW/glfw3.h",
        "include/GLFW/glfw3native.h",
    ],
    includes = ["include"],
    visibility = [
        "//visibility:private",
    ],
)

COMMON_HDRS = [
    "include/GLFW/glfw3.h",
    "include/GLFW/glfw3native.h",
    "src/internal.h",
    "src/mappings.h",
    "src/null_joystick.h",
    "src/null_platform.h",
    "src/xkb_unicode.h",
    "src/platform.h",
]

COMMON_SRCS = [
    "src/context.c",
    "src/egl_context.c",
    "src/init.c",
    "src/input.c",
    "src/osmesa_context.c",
    "src/monitor.c",
    "src/null_init.c",
    "src/null_window.c",
    "src/null_monitor.c",
    "src/null_joystick.c",
    "src/vulkan.c",
    "src/window.c",
    "src/xkb_unicode.c",
    "src/platform.c",
]

DARWIN_HDRS = [
    "src/cocoa_joystick.h",
    "src/cocoa_platform.h",
    "src/cocoa_time.h",
    "src/posix_thread.h",
    "src/wl_platform.h",
]

DARWIN_SRCS = [
    "src/cocoa_init.m",
    "src/cocoa_time.c",
    "src/cocoa_joystick.m",
    "src/cocoa_monitor.m",
    "src/cocoa_window.m",
    "src/nsgl_context.m",
    "src/posix_module.c",
    "src/posix_poll.c",
    "src/posix_thread.c",
]

LINUX_HDRS = [
    "src/linux_joystick.h",
    "src/posix_poll.h",
    "src/posix_thread.h",
    "src/posix_time.h",
    "src/x11_platform.h",
]

LINUX_SRCS = [
    "src/glx_context.c",
    "src/linux_joystick.c",
    "src/posix_module.c",
    "src/posix_poll.c",
    "src/posix_thread.c",
    "src/posix_time.c",
    "src/x11_init.c",
    "src/x11_monitor.c",
    "src/x11_window.c",
]

WIN32_SRCS = [
    "src/win32_init.c",
    "src/win32_joystick.c",
    "src/win32_module.c",
    "src/win32_monitor.c",
    "src/win32_thread.c",
    "src/win32_time.c",
    "src/win32_window.c",
    "src/wgl_context.c",
]

WIN32_HDRS = [
    "src/win32_joystick.h",
    "src/win32_platform.h",
    "src/win32_thread.h",
    "src/win32_time.h",
]

objc_library(
    name = "glfw-cocoa",
    srcs = COMMON_SRCS + DARWIN_SRCS,
    hdrs = COMMON_HDRS + DARWIN_HDRS,
    copts = [
        "-fno-objc-arc",
    ],
    defines = [
        "_GLFW_COCOA",
        "GLFW_INVALID_CODEPOINT",
    ],
    linkopts = [
        "-framework OpenGL",
        "-framework Cocoa",
        "-framework IOKit",
        "-framework CoreFoundation",
    ],
    target_compatible_with = ["@platforms//os:macos"],
    visibility = ["//visibility:private"],
)

cc_library(
    name = "glfw-linux",
    srcs = COMMON_SRCS + LINUX_SRCS,
    hdrs = COMMON_HDRS + LINUX_HDRS,
    defines = ["_GLFW_X11"],
    target_compatible_with = ["@platforms//os:linux"],
    visibility = ["//visibility:private"],
    deps = [
        "@libx11",
        "@libxcursor",
        "@libxi",
        "@libxinerama",
        "@libxrandr",
    ],
)

cc_library(
    name = "glfw-windows",
    srcs = COMMON_SRCS + WIN32_SRCS,
    hdrs = COMMON_HDRS + WIN32_HDRS,
    defines = ["_GLFW_WIN32"],
    linkopts = [
        "-DEFAULTLIB:user32.lib",
        "-DEFAULTLIB:gdi32.lib",
        "-DEFAULTLIB:shell32.lib",
    ],
    target_compatible_with = ["@platforms//os:windows"],
    visibility = ["//visibility:private"],
)

cc_library(
    name = "glfw",
    visibility = ["//visibility:public"],
    deps = select({
        "@platforms//os:linux": [
            ":glfw-headers",
            ":glfw-linux",
        ],
        "@platforms//os:macos": [
            ":glfw-cocoa",
            ":glfw-headers",
        ],
        "@platforms//os:windows": [
            "glfw-windows",
            ":glfw-headers",
        ],
    }),
)

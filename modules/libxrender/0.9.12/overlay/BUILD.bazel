load("@bazel_skylib//rules:expand_template.bzl", "expand_template")
load("@rules_cc//cc:cc_library.bzl", "cc_library")

UPSTREAM_VERSION = module_version().split(".bcr.", 1)[0]

UPSTREAM_VERSION_MAJOR = UPSTREAM_VERSION.split(".")[0]

UPSTREAM_VERSION_MINOR = UPSTREAM_VERSION.split(".")[1]

UPSTREAM_VERSION_PATCH = UPSTREAM_VERSION.split(".")[2]

expand_template(
    name = "config_h",
    out = "src/config.h",
    substitutions = {
        "{LIBXRENDER_VERSION}": UPSTREAM_VERSION,
        "{LIBXRENDER_VERSION_MAJOR}": UPSTREAM_VERSION_MAJOR,
        "{LIBXRENDER_VERSION_MINOR}": UPSTREAM_VERSION_MINOR,
        "{LIBXRENDER_VERSION_PATCH}": UPSTREAM_VERSION_PATCH,
    },
    template = "src/config.h.in",
)

cc_library(
    name = "xrender",
    srcs = glob([
        "src/*.c",
        "src/*.h",
    ]) + [
        "src/config.h",
    ],
    hdrs = glob(["include/X11/extensions/*.h"]),
    copts = [
        "-DHAVE_CONFIG_H",
    ],
    includes = [
        "include",
        "include/X11/extensions",
        "src",
    ],
    deps = [
        "@libx11",
    ],
)

alias(
    name = "libxrender",
    actual = ":xrender",
    visibility = ["//visibility:public"],
)

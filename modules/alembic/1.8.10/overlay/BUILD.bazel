load("@bazel_skylib//rules:expand_template.bzl", "expand_template")
load("@rules_cc//cc:cc_library.bzl", "cc_library")

expand_template(
    name = "Config_h",
    out = "alembic/lib/Alembic/Util/Config.h",
    substitutions = {
        "${PROJECT_VERSION_MAJOR}": "1",
        "${PROJECT_VERSION_MINOR}": "8",
        "${PROJECT_VERSION_PATCH}": "10",
        "#cmakedefine ALEMBIC_WITH_HDF5": "/* #undef ALEMBIC_WITH_HDF5 */",
    },
    template = "alembic/lib/Alembic/Util/Config.h.in",
    visibility = ["//:__pkg__"],
)

cc_library(
    name = "alembic",
    srcs = glob(
        [
            "alembic/lib/Alembic/**/*.cpp",
        ],
        exclude = [
            # HDF5 is outdated and replaced by ogawa.
            "alembic/lib/Alembic/AbcCoreHDF5/**",
            "alembic/lib/Alembic/**/Tests/**",
            "alembic/lib/Alembic/**/Examples/**",
        ],
    ),
    hdrs = glob(["alembic/lib/Alembic/**/*.h"]) + [":Config_h"],
    # You may need to define ALEMBIC_LIB_USES_BOOST if not using C++11/14
    copts = ["-DALEMBIC_ILMBASE_IMATH_2_0"],
    includes = ["alembic/lib"],
    visibility = ["//visibility:public"],
    deps = [
        "@imath",
        "@zlib",
    ],
)

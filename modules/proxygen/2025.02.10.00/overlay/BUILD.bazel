load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_library", "cc_test")
load("@rules_license//rules:license.bzl", "license")
load("@rules_python//python:defs.bzl", "py_binary")

package(default_applicable_licenses = [":license"])

license(
    name = "license",
    license_kinds = ["@rules_license//licenses/spdx:BSD-3-Clause"],
    license_text = "LICENSE",
    visibility = ["//visibility:public"],
)

# NOTE: This is one big library because some of the headers have complex circular dependencies.
cc_library(
    name = "proxygen",
    srcs = glob(
        ["proxygen/lib/**/*.cpp"],
        exclude = [
            # This library doesn't compile and is not included in the CMakeLists.txt.
            "proxygen/lib/dns/**",
            # This is a binary tool, not part of the lib.
            "proxygen/lib/http/codec/compress/experimental/interop/**",
            # This is a binary tool, not part of the lib.
            "proxygen/lib/http/codec/compress/experimental/simulator/**",
            # The stats lib depends on fb303 which is heavy-weight and not yet in the BCR.
            "proxygen/lib/stats/**",
            # Disabled because it depends on the stats lib.
            "proxygen/lib/http/stats/**",
            # Disabled because it depends on the stats lib.
            "proxygen/lib/ssl/ThreadLocalSSLStats.cpp",
            "proxygen/lib/**/test/**",
        ],
    ) + [
        # Generated by :gen_http_common_headers_srcs
        "proxygen/lib/http/HTTPCommonHeaders.cpp",
        # Generated by :gen_trace_event_constants_srcs
        "proxygen/lib/utils/TraceEventType.cpp",
        # Generated by :gen_trace_event_constants_srcs
        "proxygen/lib/utils/TraceFieldType.cpp",
    ],
    hdrs = glob(
        ["proxygen/lib/**/*.h"],
        exclude = [
            # This library doesn't compile and is not included in the CMakeLists.txt.
            "proxygen/lib/dns/**",
            "proxygen/lib/utils/perfect_hash_table_template.h",
            # The stats lib depends on fb303 which is heavy-weight and not yet in the BCR.
            "proxygen/lib/stats/**",
            # Disabled because it depends on the stats lib.
            "proxygen/lib/http/stats/**",
            # Disabled because it depends on the stats lib.
            "proxygen/lib/ssl/ThreadLocalSSLStats.h",
            "proxygen/lib/**/test/**",
        ],
    ) + [
        # Generated by :gen_http_common_headers_srcs:
        "proxygen/lib/http/HTTPCommonHeaders.h",
        # Generated by :gen_trace_event_constants_srcs
        "proxygen/lib/utils/TraceEventType.h",
        # Generated by :gen_trace_event_constants_srcs
        "proxygen/lib/utils/TraceFieldType.h",
    ],
    includes = ["."],
    # Silence #pragma warnings about deprecated boost bind usage.
    local_defines = ["BOOST_BIND_GLOBAL_PLACEHOLDERS"],
    visibility = ["//visibility:public"],
    deps = [
        ":external_http_parser",
        ":gen_http_common_headers_srcs",
        ":gen_trace_event_constants_srcs",
        "@boost.algorithm",
        "@boost.bind",
        "@boost.filesystem",
        "@boost.iostreams",
        "@boost.lexical_cast",
        "@boost.thread",
        "@boost.variant",
        "@fizz",
        "@fmt",
        "@folly//folly:base64",
        "@folly//folly:cancellation_token",
        "@folly//folly:conv",
        "@folly//folly:cpp_attributes",
        "@folly//folly:exception_wrapper",
        "@folly//folly:expected",
        "@folly//folly:fbstring",
        "@folly//folly:file_util",
        "@folly//folly:format",
        "@folly//folly:function",
        "@folly//folly:indestructible",
        "@folly//folly:intrusive_list",
        "@folly//folly:memory",
        "@folly//folly:network_address",
        "@folly//folly:observer_container",
        "@folly//folly:optional",
        "@folly//folly:overload",
        "@folly//folly:portability",
        "@folly//folly:random",
        "@folly//folly:range",
        "@folly//folly:scope_guard",
        "@folly//folly:singleton",
        "@folly//folly:singleton_thread_local",
        "@folly//folly:small_vector",
        "@folly//folly:string",
        "@folly//folly:synchronized",
        "@folly//folly:thread_local",
        "@folly//folly:try",
        "@folly//folly:utility",
        "@folly//folly/compression",
        "@folly//folly/container:evicting_cache_map",
        "@folly//folly/container:f14_hash",
        "@folly//folly/container:foreach",
        "@folly//folly/container:reserve",
        "@folly//folly/experimental/io:io_uring_backend",
        "@folly//folly/experimental/symbolizer",
        "@folly//folly/futures:core",
        "@folly//folly/hash",
        "@folly//folly/io:iobuf",
        "@folly//folly/io:socket_option_map",
        "@folly//folly/io/async:async_base",
        "@folly//folly/io/async:async_socket",
        "@folly//folly/io/async:async_socket_exception",
        "@folly//folly/io/async:async_ssl_socket",
        "@folly//folly/io/async:async_transport",
        "@folly//folly/io/async:async_udp_socket",
        "@folly//folly/io/async:delayed_destruction",
        "@folly//folly/io/async:destructor_check",
        "@folly//folly/io/async:event_base_manager",
        "@folly//folly/io/async:server_socket",
        "@folly//folly/io/async:ssl_context",
        "@folly//folly/json:dynamic",
        "@folly//folly/lang:assume",
        "@folly//folly/lang:bits",
        "@folly//folly/lang:exception",
        "@folly//folly/logging",
        "@folly//folly/portability:gflags",
        "@folly//folly/portability:openssl",
        "@folly//folly/portability:sockets",
        "@folly//folly/portability:stdlib",
        "@folly//folly/portability:sys_resource",
        "@folly//folly/portability:time",
        "@folly//folly/portability:unistd",
        "@folly//folly/portability:windows",
        "@folly//folly/ssl:openssl_hash",
        "@folly//folly/ssl:ssl_session",
        "@folly//folly/synchronization:rcu",
        "@folly//folly/tracing:scoped_trace_section",
        "@gflags",
        "@glog",
        "@mvfst//:quic",
        "@openssl//:ssl",
        "@wangle//:acceptor",
        "@wangle//:client_persistence",
        "@wangle//:ssl",
        "@zlib",
    ],
)

cc_library(
    name = "httpserver",
    srcs = glob(
        ["proxygen/httpserver/**/*.cpp"],
        exclude = [
            "proxygen/httpserver/samples/**",
            "proxygen/httpserver/**/tests/**",
        ],
    ),
    hdrs = glob(
        ["proxygen/httpserver/**/*.h"],
        exclude = [
            "proxygen/httpserver/samples/**",
            "proxygen/httpserver/Mocks.h",
        ],
    ),
    includes = ["."],
    visibility = ["//visibility:public"],
    deps = [
        ":proxygen",
        "@boost.thread",
        "@folly//folly:exception_string",
        "@folly//folly:expected",
        "@folly//folly:function",
        "@folly//folly:network_address",
        "@folly//folly:range",
        "@folly//folly:scope_guard",
        "@folly//folly/executors:io_thread_pool_executor",
        "@folly//folly/executors/thread_factory:named_thread_factory",
        "@folly//folly/io/async:async_signal_handler",
        "@folly//folly/io/async:event_base_manager",
        "@folly//folly/io/async:server_socket",
        "@folly//folly/io/async:ssl_context",
        "@folly//folly/system:thread_name",
        "@wangle//:bootstrap",
        "@wangle//:ssl",
    ],
)

cc_library(
    name = "external_http_parser",
    srcs = ["proxygen/external/http_parser/http_parser_cpp.cpp"],
    hdrs = ["proxygen/external/http_parser/http_parser.h"],
    visibility = ["//visibility:public"],
)

genrule(
    name = "gen_http_common_headers_srcs",
    srcs = [
        "proxygen/lib/http/gen_HTTPCommonHeaders.sh",
        "proxygen/lib/utils/gen_perfect_hash_table.sh",
        "proxygen/lib/utils/perfect_hash_table_template.h",
        "proxygen/lib/utils/perfect_hash_table_template.cpp.gperf",
        "proxygen/lib/http/HTTPCommonHeaders.txt",
        "BUILD.bazel",
    ],
    outs = [
        "proxygen/lib/http/HTTPCommonHeaders.h",
        "proxygen/lib/http/HTTPCommonHeaders.cpp",
    ],
    cmd = "mkdir -p $(RULEDIR)/proxygen/lib/http && " +
          "HEADERS_LIST=$(execpath proxygen/lib/http/HTTPCommonHeaders.txt) " +
          "INSTALL_DIR=$(RULEDIR)/proxygen/lib/http " +
          "FBCODE_DIR=$$(dirname $(execpath //:BUILD.bazel)) " +
          "OUTPUT_DIR=$(RULEDIR)/proxygen/lib/http " +
          "GPERF=$(execpath @gperf) " +
          "$(execpath proxygen/lib/http/gen_HTTPCommonHeaders.sh) " +
          "$(execpath proxygen/lib/http/HTTPCommonHeaders.txt)",
    tools = ["@gperf"],
)

py_binary(
    name = "gen_trace_event_constants",
    srcs = ["proxygen/lib/utils/gen_trace_event_constants.py"],
    visibility = ["//visibility:public"],
)

genrule(
    name = "gen_trace_event_constants_srcs",
    srcs = [
        "proxygen/lib/utils/samples/TraceEventType.txt",
        "proxygen/lib/utils/samples/TraceFieldType.txt",
    ],
    outs = [
        "proxygen/lib/utils/TraceEventType.h",
        "proxygen/lib/utils/TraceEventType.cpp",
        "proxygen/lib/utils/TraceFieldType.h",
        "proxygen/lib/utils/TraceFieldType.cpp",
    ],
    cmd = "mkdir -p $(RULEDIR)/proxygen/lib/utils && " +
          "$(execpath :gen_trace_event_constants) " +
          "--output_type=cpp " +
          "--input_files=$(execpath proxygen/lib/utils/samples/TraceEventType.txt)," +
          "$(execpath proxygen/lib/utils/samples/TraceFieldType.txt) " +
          "--output_scope=proxygen " +
          "--header_path=proxygen/lib/utils " +
          "--fbcode_dir=UNUSED " +
          "--install_dir=$(RULEDIR)/proxygen/lib/utils",
    tools = [":gen_trace_event_constants"],
)

###################################################################################################
# Tests
###################################################################################################

cc_library(
    name = "proxygen_test_main",
    srcs = ["proxygen/lib/test/TestMain.cpp"],
    visibility = ["//visibility:public"],
    deps = [
        "@folly//folly/portability:gflags",
        "@folly//folly/portability:gtest",
        "@glog",
    ],
)

cc_library(
    name = "proxygen_test_lib",
    srcs = glob(
        ["proxygen/lib/**/test/*.cpp"],
        exclude = [
            "proxygen/lib/dns/**",
            "proxygen/lib/stats/**",
            "proxygen/lib/test/TestMain.cpp",
            "proxygen/lib/**/test/*Bench.cpp",
            "proxygen/lib/**/test/*Benchmark.cpp",
            "proxygen/lib/**/test/*Test.cpp",
            "proxygen/lib/**/test/*Tests.cpp",
        ],
    ),
    hdrs = glob(
        ["proxygen/lib/**/test/*.h"],
        exclude = [
            "proxygen/lib/dns/**",
            "proxygen/lib/stats/**",
        ],
    ),
    visibility = ["//visibility:public"],
    deps = [
        ":proxygen",
        "@boost.optional",
        "@folly//folly:exception_wrapper",
        "@folly//folly:expected",
        "@folly//folly:format",
        "@folly//folly:function",
        "@folly//folly:memory",
        "@folly//folly:network_address",
        "@folly//folly:optional",
        "@folly//folly:random",
        "@folly//folly:string",
        "@folly//folly/futures:core",
        "@folly//folly/io:iobuf",
        "@folly//folly/io/async:async_base",
        "@folly//folly/io/async:async_socket_exception",
        "@folly//folly/io/async:async_transport",
        "@folly//folly/io/async:async_transport_certificate",
        "@folly//folly/io/async:event_base_manager",
        "@folly//folly/io/async/ssl:openssl_transport_certificate",
        "@folly//folly/io/async/test:mocks",
        "@folly//folly/json:dynamic",
        "@folly//folly/portability:gmock",
        "@folly//folly/portability:gtest",
        "@glog",
        "@googletest//:gtest",
        "@mvfst//:quic",
        "@mvfst//:quic_test_lib",
        "@wangle//:acceptor",
    ],
)

[cc_test(
    name = test_file.removesuffix(".cpp").replace("/", "_"),
    size = "small",
    srcs = [test_file],
    data = [
        "proxygen/lib/http/session/test/test_cert1.key",
        "proxygen/lib/http/session/test/test_cert1.pem",
    ],
    deps = [
        ":proxygen",
        ":proxygen_test_lib",
        ":proxygen_test_main",
        "@boost.algorithm",
        "@boost.container",
        "@boost.optional",
        "@fizz",
        "@folly//folly:base64",
        "@folly//folly:conv",
        "@folly//folly:expected",
        "@folly//folly:file_util",
        "@folly//folly:format",
        "@folly//folly:random",
        "@folly//folly:range",
        "@folly//folly:scope_guard",
        "@folly//folly:singleton",
        "@folly//folly:string",
        "@folly//folly/container:foreach",
        "@folly//folly/executors:io_thread_pool_executor",
        "@folly//folly/futures:core",
        "@folly//folly/io:iobuf",
        "@folly//folly/io/async:async_base",
        "@folly//folly/io/async:async_ssl_socket",
        "@folly//folly/io/async:event_base_manager",
        "@folly//folly/io/async:server_socket",
        "@folly//folly/io/async/test:mock_server_socket",
        "@folly//folly/io/async/test:mocks",
        "@folly//folly/io/async/test:util",
        "@folly//folly/logging",
        "@folly//folly/portability:gflags",
        "@folly//folly/portability:gmock",
        "@folly//folly/portability:gtest",
        "@folly//folly/synchronization:baton",
        "@glog",
        "@mvfst//:quic",
        "@wangle//:acceptor",
        "@wangle//:client_persistence_test_lib",
    ],
) for test_file in glob(
    [
        "proxygen/lib/**/test/*Test.cpp",
        "proxygen/lib/**/test/*Tests.cpp",
    ],
    exclude = [
        "proxygen/lib/dns/**",
        "proxygen/lib/stats/**",
        # This does not compile. It seems like an internal facebook file that escaped.
        "proxygen/lib/http/sink/test/HTTPConnectSinkTest.cpp",
    ],
)]

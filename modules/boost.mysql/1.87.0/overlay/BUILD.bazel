load("@bazel_skylib//rules:common_settings.bzl", "string_flag")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@rules_cc//cc:defs.bzl", "cc_library")

package(default_visibility = ["//visibility:public"])

string_flag(
    name = "ssl",
    build_setting_default = "no_ssl",
    values = [
        "no_ssl",
        "openssl",
        "boringssl",
    ],
    visibility = ["//visibility:public"],
)

config_setting(
    name = "no_ssl",
    flag_values = {":ssl": "no_ssl"},
)

config_setting(
    name = "openssl",
    flag_values = {":ssl": "openssl"},
)

config_setting(
    name = "boringssl",
    flag_values = {":ssl": "boringssl"},
)

write_file(
    name = "src",
    out = "boost.mysql.src.cpp",
    content = [
        "#include <boost/mysql/impl/static_results_impl.ipp>",
        "#include <boost/mysql/impl/static_execution_state_impl.ipp>",
        "#include <boost/mysql/impl/date.ipp>",
        "#include <boost/mysql/impl/error_categories.ipp>",
        "#include <boost/mysql/impl/escape_string.ipp>",
        "#include <boost/mysql/impl/field_view.ipp>",
        "#include <boost/mysql/impl/row_impl.ipp>",
        "#include <boost/mysql/impl/is_fatal_error.ipp>",
        "#include <boost/mysql/impl/column_type.ipp>",
        "#include <boost/mysql/impl/internal/auth/auth.ipp>",
        "#include <boost/mysql/impl/internal/error/server_error_to_string.ipp>",
        "#include <boost/mysql/impl/engine_impl_instantiations.ipp>",
        "#include <boost/mysql/impl/pipeline.ipp>",
        "#include <boost/mysql/impl/connection_impl.ipp>",
        "#include <boost/mysql/impl/character_set.ipp>",
        "#include <boost/mysql/impl/format_sql.ipp>",
        "#include <boost/mysql/impl/execution_state_impl.ipp>",
        "#include <boost/mysql/impl/resultset.ipp>",
        "#include <boost/mysql/impl/connection_pool.ipp>",
        "#include <boost/mysql/impl/field.ipp>",
        "#include <boost/mysql/impl/any_connection.ipp>",
        "#include <boost/mysql/impl/results_impl.ipp>",
        "#include <boost/mysql/impl/datetime.ipp>",
        "#include <boost/mysql/impl/meta_check_context.ipp>",
        "#include <boost/mysql/impl/field_kind.ipp>",
    ],
)

cc_library(
    name = "boost.mysql",
    srcs = [":src"],
    hdrs = glob([
        "include/**/*.*pp",
    ]),
    defines = ["BOOST_MYSQL_SEPARATE_COMPILATION"],
    features = [
        "parse_headers",
    ],
    includes = ["include"],
    deps = [
        "@boost.align",
        "@boost.array",
        "@boost.asio",
        "@boost.assert",
        "@boost.bind",
        "@boost.charconv",
        "@boost.chrono",
        "@boost.compat",
        "@boost.config",
        "@boost.context",
        "@boost.core",
        "@boost.coroutine",
        "@boost.date_time",
        "@boost.endian",
        "@boost.exception",
        "@boost.function",
        "@boost.pfr",
        "@boost.regex",
        "@boost.smart_ptr",
        "@boost.system",
        "@boost.throw_exception",
        "@boost.type_traits",
        "@boost.utility",
    ] + select({
        ":openssl": [
            "@openssl//:crypto",
            "@openssl//:ssl",
        ],
        ":boringssl": [
            "@boringssl//:crypto",
            "@boringssl//:ssl",
        ],
        ":no_ssl": [],
    }),
)

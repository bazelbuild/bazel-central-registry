"""Bazel build file for asio-grpc (https://github.com/Tradias/asio-grpc)."""

load("@grpc//bazel:cc_grpc_library.bzl", "cc_grpc_library")
load("@protobuf//bazel:cc_proto_library.bzl", "cc_proto_library")
load("@protobuf//bazel:proto_library.bzl", "proto_library")
load("@rules_cc//cc:defs.bzl", "cc_library", "cc_test")
load("@rules_license//rules:license.bzl", "license")

package(default_applicable_licenses = [":license"])

license(
    name = "license",
    license_kinds = ["@rules_license//licenses/spdx:Apache-2.0"],
    license_text = "LICENSE",
)

# NOTE: Unifex is not supported yet.

cc_library(
    name = "asio-grpc_boost_asio",
    hdrs = glob(["src/agrpc/**/*.hpp"]),
    defines = ["AGRPC_BOOST_ASIO"],
    features = ["parse_headers"],
    includes = ["src"],
    textual_hdrs = glob(["src/agrpc/**/*.ipp"]),
    visibility = ["//visibility:public"],
    deps = [
        "@boost.asio",
        "@grpc//:grpc++",
    ],
)

cc_library(
    name = "asio-grpc_standalone_asio",
    hdrs = glob(["src/agrpc/**/*.hpp"]),
    defines = ["AGRPC_STANDALONE_ASIO"],
    features = ["parse_headers"],
    includes = ["src"],
    textual_hdrs = glob(["src/agrpc/**/*.ipp"]),
    visibility = ["//visibility:public"],
    deps = [
        "@asio",
        "@grpc//:grpc++",
    ],
)

cc_test(
    name = "asio-grpc_boost_asio_cpp20_test",
    size = "small",
    srcs = glob(
        ["test/src/*_20.cpp"],
        exclude = ["test/src/test_unifex_20.cpp"],
    ),
    copts = ['-DASIO_GRPC_TEST_CPP_VERSION=\'\"Boost.Asio C++20\"\''],
    deps = [
        ":asio-grpc_boost_asio",
        ":test_service_cc_gprc",
        ":test_utils_boost_asio",
        "@boost.coroutine",
        "@boost.interprocess",
        "@boost.thread",
        "@grpc//:grpc++",
    ],
)

cc_test(
    name = "asio-grpc_standalone_asio_cpp20_test",
    size = "small",
    srcs = glob(
        ["test/src/*_20.cpp"],
        exclude = ["test/src/test_unifex_20.cpp"],
    ),
    copts = ['-DASIO_GRPC_TEST_CPP_VERSION=\'\"Standalone Asio C++20\"\''],
    deps = [
        ":asio-grpc_standalone_asio",
        ":test_service_cc_gprc",
        ":test_utils_standalone_asio",
        "@boost.coroutine",
        "@boost.thread",
        "@grpc//:grpc++",
    ],
)

cc_test(
    name = "asio-grpc_boost_asio_cpp17_test",
    size = "small",
    srcs = glob(
        ["test/src/*_17.cpp"],
        exclude = ["test/src/test_test_17.cpp"],
    ),
    copts = ['-DASIO_GRPC_TEST_CPP_VERSION=\'\"Boost.Asio C++17\"\''],
    deps = [
        ":asio-grpc_boost_asio",
        ":health_service_cc_gprc",
        ":test_service_cc_gprc",
        ":test_utils_boost_asio",
        "@boost.coroutine",
        "@boost.interprocess",
        "@boost.thread",
        "@grpc//:grpc++",
    ],
)

cc_test(
    name = "asio-grpc_standalone_asio_cpp17_test",
    size = "small",
    srcs = glob(
        ["test/src/*_17.cpp"],
        exclude = ["test/src/test_test_17.cpp"],
    ),
    copts = ['-DASIO_GRPC_TEST_CPP_VERSION=\'\"Standalone Asio C++17\"\''],
    deps = [
        ":asio-grpc_standalone_asio",
        ":health_service_cc_gprc",
        ":test_service_cc_gprc",
        ":test_utils_standalone_asio",
        "@boost.coroutine",
        "@boost.thread",
        "@grpc//:grpc++",
    ],
)

cc_library(
    name = "test_utils_boost_asio",
    srcs = glob(["test/utils/utils/*.cpp"]),
    hdrs = glob(["test/utils/utils/*.hpp"]),
    defines = [
        "BOOST_ASIO_NO_TS_EXECUTORS",
        "BOOST_ASIO_SEPARATE_COMPILATION",
    ],
    includes = ["test/utils"],
    deps = [
        ":asio-grpc_boost_asio",
        ":test_service_cc_gprc",
        "@boost.asio",
        "@boost.filesystem",
        "@boost.interprocess",
        "@doctest//doctest",
        "@googletest//:gtest",
        "@grpc//:grpc++",
        "@protobuf",
    ],
)

cc_library(
    name = "test_utils_standalone_asio",
    srcs = glob(["test/utils/utils/*.cpp"]),
    hdrs = glob(["test/utils/utils/*.hpp"]),
    defines = [
        "ASIO_SEPARATE_COMPILATION",
        "ASIO_NO_TS_EXECUTORS",
    ],
    includes = ["test/utils"],
    deps = [
        ":asio-grpc_standalone_asio",
        ":test_service_cc_gprc",
        "@asio",
        "@boost.filesystem",
        "@boost.interprocess",
        "@doctest//doctest",
        "@googletest//:gtest",
        "@grpc//:grpc++",
        "@protobuf",
    ],
)

proto_library(
    name = "test_message_proto",
    srcs = ["test/proto/test/msg/message.proto"],
    strip_import_prefix = "test/proto",
)

cc_proto_library(
    name = "test_message_cc_proto",
    deps = [":test_message_proto"],
)

proto_library(
    name = "test_service_proto",
    srcs = ["test/proto/test/v1/test.proto"],
    deps = [":test_message_proto"],
)

cc_proto_library(
    name = "test_service_cc_proto",
    deps = [":test_service_proto"],
)

cc_grpc_library(
    name = "test_service_cc_gprc",
    srcs = [":test_service_proto"],
    grpc_only = True,
    deps = [":test_service_cc_proto"],
)

proto_library(
    name = "health_service_proto",
    srcs = ["test/proto/grpc/health/v1/health.proto"],
)

cc_proto_library(
    name = "health_service_cc_proto",
    deps = [":health_service_proto"],
)

cc_grpc_library(
    name = "health_service_cc_gprc",
    srcs = [":health_service_proto"],
    grpc_only = True,
    deps = [":health_service_cc_proto"],
)

load("@rules_cc//cc:defs.bzl", "cc_library")
load("//:defs.bzl", "AVX2_COPTS", "OPUS_COPTS", "OPUS_INCLUDES", "OPUS_LINKOPTS", "SSE2_COPTS", "SSE4_1_COPTS", "SSE_COPTS")

package(default_visibility = ["//:__subpackages__"])

cc_library(
    name = "config",
    hdrs = ["config.h"],
    deps = ["@rules_autoconf//:config"],
    defines = [
        "HAVE_CONFIG_H",
    ] + select({
        "@platforms//cpu:arm64": [
            "OPUS_ARM_MAY_HAVE_NEON_INTR",
            "OPUS_ARM_PRESUME_NEON_INTR",
        ],
        "@platforms//cpu:x86_64": [
            "OPUS_HAVE_RTCD",
            "CPU_INFO_BY_ASM",
            "OPUS_X86_MAY_HAVE_SSE",
            "OPUS_X86_PRESUME_SSE",
            "OPUS_X86_MAY_HAVE_SSE2",
            "OPUS_X86_PRESUME_SSE2",
            "OPUS_X86_MAY_HAVE_SSE4_1",
            "OPUS_X86_MAY_HAVE_AVX2",
        ],
        "//conditions:default": [],
    }),
)

cc_library(
    name = "opus_headers",
    hdrs = glob(["include/*.h"]),
    includes = ["include"],
)

INTERNAL_HEADERS = glob(["celt/**/*.h", "silk/**/*.h"])

cc_library(
    name = "celt_arm_neon",
    srcs = [
        "celt/arm/armcpu.c",
        "celt/arm/arm_celt_map.c",
        "celt/arm/celt_neon_intr.c",
        "celt/arm/pitch_neon_intr.c",
    ] + INTERNAL_HEADERS,
    copts = OPUS_COPTS,
    includes = OPUS_INCLUDES,
    deps = [
        ":config",
        ":opus_headers",
    ],
    linkopts = OPUS_LINKOPTS,
    alwayslink = True,
    target_compatible_with = ["@platforms//cpu:arm64"],
    visibility = ["//visibility:private"],
)

cc_library(
    name = "celt_x86_rtcd",
    srcs = [
        "celt/x86/x86cpu.c",
        "celt/x86/x86_celt_map.c",
    ] + INTERNAL_HEADERS,
    copts = OPUS_COPTS,
    includes = OPUS_INCLUDES,
    deps = [
        ":config",
        ":opus_headers",
    ],
    linkopts = OPUS_LINKOPTS,
    alwayslink = True,
    target_compatible_with = ["@platforms//cpu:x86_64"],
    visibility = ["//visibility:private"],
)

cc_library(
    name = "celt_sse",
    srcs = ["celt/x86/pitch_sse.c"] + INTERNAL_HEADERS,
    copts = OPUS_COPTS + SSE_COPTS,
    includes = OPUS_INCLUDES,
    deps = [
        ":config",
        ":opus_headers",
    ],
    linkopts = OPUS_LINKOPTS,
    alwayslink = True,
    target_compatible_with = ["@platforms//cpu:x86_64"],
    visibility = ["//visibility:private"],
)

cc_library(
    name = "celt_sse2",
    srcs = [
        "celt/x86/pitch_sse2.c",
        "celt/x86/vq_sse2.c",
    ] + INTERNAL_HEADERS,
    copts = OPUS_COPTS + SSE2_COPTS,
    includes = OPUS_INCLUDES,
    deps = [
        ":config",
        ":opus_headers",
    ],
    linkopts = OPUS_LINKOPTS,
    alwayslink = True,
    target_compatible_with = ["@platforms//cpu:x86_64"],
    visibility = ["//visibility:private"],
)

cc_library(
    name = "celt_sse4_1",
    srcs = [
        "celt/x86/celt_lpc_sse4_1.c",
        "celt/x86/pitch_sse4_1.c",
    ] + INTERNAL_HEADERS,
    copts = OPUS_COPTS + SSE4_1_COPTS,
    includes = OPUS_INCLUDES,
    deps = [
        ":config",
        ":opus_headers",
    ],
    linkopts = OPUS_LINKOPTS,
    alwayslink = True,
    target_compatible_with = ["@platforms//cpu:x86_64"],
    visibility = ["//visibility:private"],
)

cc_library(
    name = "celt_avx2",
    srcs = ["celt/x86/pitch_avx.c"] + INTERNAL_HEADERS,
    copts = OPUS_COPTS + AVX2_COPTS,
    includes = OPUS_INCLUDES,
    deps = [
        ":config",
        ":opus_headers",
    ],
    linkopts = OPUS_LINKOPTS,
    alwayslink = True,
    target_compatible_with = ["@platforms//cpu:x86_64"],
    visibility = ["//visibility:private"],
)

cc_library(
    name = "silk_arm_neon",
    srcs = [
        "silk/arm/arm_silk_map.c",
        "silk/arm/biquad_alt_neon_intr.c",
        "silk/arm/LPC_inv_pred_gain_neon_intr.c",
        "silk/arm/NSQ_del_dec_neon_intr.c",
        "silk/arm/NSQ_neon.c",
    ] + INTERNAL_HEADERS,
    copts = OPUS_COPTS,
    includes = OPUS_INCLUDES,
    deps = [
        ":config",
        ":opus_headers",
    ],
    linkopts = OPUS_LINKOPTS,
    alwayslink = True,
    target_compatible_with = ["@platforms//cpu:arm64"],
    visibility = ["//visibility:private"],
)

cc_library(
    name = "silk_x86_rtcd",
    srcs = ["silk/x86/x86_silk_map.c"] + INTERNAL_HEADERS,
    copts = OPUS_COPTS,
    includes = OPUS_INCLUDES,
    deps = [
        ":config",
        ":opus_headers",
    ],
    linkopts = OPUS_LINKOPTS,
    alwayslink = True,
    target_compatible_with = ["@platforms//cpu:x86_64"],
    visibility = ["//visibility:private"],
)

cc_library(
    name = "silk_sse4_1",
    srcs = [
        "silk/x86/NSQ_sse4_1.c",
        "silk/x86/NSQ_del_dec_sse4_1.c",
        "silk/x86/VAD_sse4_1.c",
        "silk/x86/VQ_WMat_EC_sse4_1.c",
    ] + INTERNAL_HEADERS,
    copts = OPUS_COPTS + SSE4_1_COPTS,
    includes = OPUS_INCLUDES,
    deps = [
        ":config",
        ":opus_headers",
    ],
    linkopts = OPUS_LINKOPTS,
    alwayslink = True,
    target_compatible_with = ["@platforms//cpu:x86_64"],
    visibility = ["//visibility:private"],
)

cc_library(
    name = "silk_avx2",
    srcs = [
        "silk/x86/NSQ_del_dec_avx2.c",
        "silk/float/x86/inner_product_FLP_avx2.c",
    ] + INTERNAL_HEADERS,
    copts = OPUS_COPTS + AVX2_COPTS,
    includes = OPUS_INCLUDES,
    deps = [
        ":config",
        ":opus_headers",
    ],
    linkopts = OPUS_LINKOPTS,
    alwayslink = True,
    target_compatible_with = ["@platforms//cpu:x86_64"],
    visibility = ["//visibility:private"],
)

cc_library(
    name = "celt",
    srcs = [
        "celt/bands.c",
        "celt/celt.c",
        "celt/celt_encoder.c",
        "celt/celt_decoder.c",
        "celt/cwrs.c",
        "celt/entcode.c",
        "celt/entdec.c",
        "celt/entenc.c",
        "celt/kiss_fft.c",
        "celt/laplace.c",
        "celt/mathops.c",
        "celt/mdct.c",
        "celt/modes.c",
        "celt/pitch.c",
        "celt/celt_lpc.c",
        "celt/quant_bands.c",
        "celt/rate.c",
        "celt/vq.c",
    ] + INTERNAL_HEADERS,
    textual_hdrs = [
        "celt/cwrs.c",
        "celt/entcode.c",
        "celt/entdec.c",
        "celt/entenc.c",
        "celt/laplace.c",
        "celt/mathops.c",
    ],
    copts = OPUS_COPTS,
    includes = OPUS_INCLUDES,
    deps = [
        ":config",
        ":opus_headers",
    ] + select({
        "@platforms//cpu:arm64": [":celt_arm_neon"],
        "@platforms//cpu:x86_64": [
            ":celt_x86_rtcd",
            ":celt_sse",
            ":celt_sse2",
            ":celt_sse4_1",
            ":celt_avx2",
        ],
        "//conditions:default": [],
    }),
    linkopts = OPUS_LINKOPTS,
)

cc_library(
    name = "silk",
    srcs = [
        "silk/CNG.c",
        "silk/code_signs.c",
        "silk/init_decoder.c",
        "silk/decode_core.c",
        "silk/decode_frame.c",
        "silk/decode_parameters.c",
        "silk/decode_indices.c",
        "silk/decode_pulses.c",
        "silk/decoder_set_fs.c",
        "silk/dec_API.c",
        "silk/enc_API.c",
        "silk/encode_indices.c",
        "silk/encode_pulses.c",
        "silk/gain_quant.c",
        "silk/interpolate.c",
        "silk/LP_variable_cutoff.c",
        "silk/NLSF_decode.c",
        "silk/NSQ.c",
        "silk/NSQ_del_dec.c",
        "silk/PLC.c",
        "silk/shell_coder.c",
        "silk/tables_gain.c",
        "silk/tables_LTP.c",
        "silk/tables_NLSF_CB_NB_MB.c",
        "silk/tables_NLSF_CB_WB.c",
        "silk/tables_other.c",
        "silk/tables_pitch_lag.c",
        "silk/tables_pulses_per_block.c",
        "silk/VAD.c",
        "silk/control_audio_bandwidth.c",
        "silk/quant_LTP_gains.c",
        "silk/VQ_WMat_EC.c",
        "silk/HP_variable_cutoff.c",
        "silk/NLSF_encode.c",
        "silk/NLSF_VQ.c",
        "silk/NLSF_unpack.c",
        "silk/NLSF_del_dec_quant.c",
        "silk/process_NLSFs.c",
        "silk/stereo_LR_to_MS.c",
        "silk/stereo_MS_to_LR.c",
        "silk/check_control_input.c",
        "silk/control_SNR.c",
        "silk/init_encoder.c",
        "silk/control_codec.c",
        "silk/A2NLSF.c",
        "silk/ana_filt_bank_1.c",
        "silk/biquad_alt.c",
        "silk/bwexpander_32.c",
        "silk/bwexpander.c",
        "silk/debug.c",
        "silk/decode_pitch.c",
        "silk/inner_prod_aligned.c",
        "silk/lin2log.c",
        "silk/log2lin.c",
        "silk/LPC_analysis_filter.c",
        "silk/LPC_inv_pred_gain.c",
        "silk/table_LSF_cos.c",
        "silk/NLSF2A.c",
        "silk/NLSF_stabilize.c",
        "silk/NLSF_VQ_weights_laroia.c",
        "silk/pitch_est_tables.c",
        "silk/resampler.c",
        "silk/resampler_down2_3.c",
        "silk/resampler_down2.c",
        "silk/resampler_private_AR2.c",
        "silk/resampler_private_down_FIR.c",
        "silk/resampler_private_IIR_FIR.c",
        "silk/resampler_private_up2_HQ.c",
        "silk/resampler_rom.c",
        "silk/sigm_Q15.c",
        "silk/sort.c",
        "silk/sum_sqr_shift.c",
        "silk/stereo_decode_pred.c",
        "silk/stereo_encode_pred.c",
        "silk/stereo_find_predictor.c",
        "silk/stereo_quant_pred.c",
        "silk/LPC_fit.c",
        "silk/float/apply_sine_window_FLP.c",
        "silk/float/corrMatrix_FLP.c",
        "silk/float/encode_frame_FLP.c",
        "silk/float/find_LPC_FLP.c",
        "silk/float/find_LTP_FLP.c",
        "silk/float/find_pitch_lags_FLP.c",
        "silk/float/find_pred_coefs_FLP.c",
        "silk/float/LPC_analysis_filter_FLP.c",
        "silk/float/LTP_analysis_filter_FLP.c",
        "silk/float/LTP_scale_ctrl_FLP.c",
        "silk/float/noise_shape_analysis_FLP.c",
        "silk/float/process_gains_FLP.c",
        "silk/float/regularize_correlations_FLP.c",
        "silk/float/residual_energy_FLP.c",
        "silk/float/warped_autocorrelation_FLP.c",
        "silk/float/wrappers_FLP.c",
        "silk/float/autocorrelation_FLP.c",
        "silk/float/burg_modified_FLP.c",
        "silk/float/bwexpander_FLP.c",
        "silk/float/energy_FLP.c",
        "silk/float/inner_product_FLP.c",
        "silk/float/k2a_FLP.c",
        "silk/float/LPC_inv_pred_gain_FLP.c",
        "silk/float/pitch_analysis_core_FLP.c",
        "silk/float/scale_copy_vector_FLP.c",
        "silk/float/scale_vector_FLP.c",
        "silk/float/schur_FLP.c",
        "silk/float/sort_FLP.c",
    ] + glob(["silk/**/*.h"]),
    copts = OPUS_COPTS,
    includes = OPUS_INCLUDES,
    deps = [
        ":celt",
        ":config",
        ":opus_headers",
    ] + select({
        "@platforms//cpu:arm64": [":silk_arm_neon"],
        "@platforms//cpu:x86_64": [
            ":silk_x86_rtcd",
            ":silk_sse4_1",
            ":silk_avx2",
        ],
        "//conditions:default": [],
    }),
    linkopts = OPUS_LINKOPTS,
)

cc_library(
    name = "opus",
    srcs = [
        "src/opus.c",
        "src/opus_decoder.c",
        "src/opus_encoder.c",
        "src/extensions.c",
        "src/opus_multistream.c",
        "src/opus_multistream_encoder.c",
        "src/opus_multistream_decoder.c",
        "src/repacketizer.c",
        "src/opus_projection_encoder.c",
        "src/opus_projection_decoder.c",
        "src/mapping_matrix.c",
        "src/analysis.c",
        "src/mlp.c",
        "src/mlp_data.c",
    ] + glob(["src/*.h"]),
    copts = OPUS_COPTS,
    includes = ["include"],
    deps = [
        ":celt",
        ":silk",
        ":opus_headers",
        ":config",
    ],
    linkopts = OPUS_LINKOPTS,
    visibility = ["//visibility:public"],
)

load("@bazel_skylib//rules:common_settings.bzl", "string_flag")
load("@rules_cc//cc:cc_library.bzl", "cc_library")

string_flag(
    name = "ssl",
    build_setting_default = "boringssl",
    values = [
        "openssl",
        "boringssl",
    ],
    visibility = ["//visibility:public"],
)

config_setting(
    name = "openssl",
    flag_values = {":ssl": "openssl"},
)

config_setting(
    name = "boringssl",
    flag_values = {":ssl": "boringssl"},
)

cc_library(
    name = "boost.mqtt5",
    hdrs = glob(
        [
            "include/**/*.hpp",
        ],
        exclude = [
            "include/boost/mqtt5/detail/**/*.hpp",
            "include/boost/mqtt5/impl/**/*.hpp",
        ],
    ),
    features = [
        "parse_headers",
    ],
    includes = ["include"],
    textual_hdrs = glob([
        "include/boost/mqtt5/detail/**/*.hpp",
        "include/boost/mqtt5/impl/**/*.hpp",
    ]),
    visibility = ["//visibility:public"],
    deps = [
        "@boost.asio",
        "@boost.assert",
        "@boost.beast",
        "@boost.container",
        "@boost.core",
        "@boost.endian",
        "@boost.random",
        "@boost.range",
        "@boost.smart_ptr",
        "@boost.spirit",
        "@boost.system",
        "@boost.type_traits",
    ] + select({
        ":boringssl": [
            "@boringssl//:crypto",
            "@boringssl//:ssl",
        ],
        ":openssl": [
            "@openssl//:crypto",
            "@openssl//:ssl",
        ],
    }),
)

# Copyright 2016 Robot Locomotion Group @ CSAIL. All rights reserved.
#
# All components of Drake are licensed under the BSD 3-Clause License.
# See LICENSE.TXT or https://drake.mit.edu/ for details.

load(
    "@cmake_configure_file//:cmake_configure_file.bzl",
    "cmake_configure_file",
)
load("@rules_cc//cc:defs.bzl", "cc_library")

licenses(["notice"])

package(
    default_visibility = ["//visibility:public"],
)

cmake_configure_file(
    name = "configure_file_types",
    src = "configure/qdldl_types.h.in",
    out = "include/qdldl_types.h",
    # https://github.com/osqp/qdldl/blob/v0.1.6/README.md#custom-types-for-integer-floats-and-booleans
    defines = [
        "QDLDL_BOOL_TYPE=unsigned char",
        # Keep the `int` type sync'd with the build of @osqp_internal and
        # @scs_internal. See drake/tools/workspace/qdldl_internal/README.md.
        "QDLDL_INT_TYPE=long long",
        # Keep the `int` type sync'd with the build of @osqp_internal and
        # @scs_internal. See drake/tools/workspace/qdldl_internal/README.md.
        "QDLDL_FLOAT_TYPE=double",
        "QDLDL_FLOAT=0",
        # Match this to QDLDL_INT_TYPE above.
        "QDLDL_INT_TYPE_MAX=LLONG_MAX",
    ],
    visibility = ["//visibility:private"],
)

cmake_configure_file(
    name = "configure_file_version",
    src = "configure/qdldl_version.h.in",
    out = "include/qdldl_version.h",
    cmakelists = [
        "CMakeLists.txt",
    ],
    visibility = ["//visibility:private"],
)

cc_library(
    name = "qdldl",
    srcs = [
        "src/qdldl.c",
    ],
    hdrs = [
        "include/qdldl.h",
        ":include/qdldl_types.h",
        ":include/qdldl_version.h",
    ],
    copts = select({
        "@platforms//os:windows": [],
        "//conditions:default": [
            "-fvisibility=hidden",
            "-Werror=incompatible-pointer-types",
        ],
    }),
    defines = [
        "BUILDING_QDLDL",
        "QDLDL_SHARED_LIB",
    ],
    includes = [
        "include",
    ],
)

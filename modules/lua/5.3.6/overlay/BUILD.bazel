load("@rules_cc//cc:cc_binary.bzl", "cc_binary")
load("@rules_cc//cc:cc_library.bzl", "cc_library")

cc_library(
    name = "lua",
    srcs = glob(
        [
            "src/*.c",
            "src/*.h",
        ],
        exclude = [
            "src/lua.c",
            "src/luac.c",
            "src/onelua.c",
        ],
    ),
    hdrs = [
        "src/lauxlib.h",
        "src/lua.h",
        "src/lua.hpp",
        "src/luaconf.h",
        "src/lualib.h",
    ],
    linkopts = select({
        "@platforms//os:linux": ["-ldl"],
        "//conditions:default": [],
    }),
    local_defines = select({
        "@platforms//os:ios": ["LUA_USE_IOS"],
        "@platforms//os:linux": ["LUA_USE_LINUX"],
        "@platforms//os:macos": ["LUA_USE_MACOSX"],
        "@platforms//os:windows": ["LUA_USE_WINDOWS"],
        "//conditions:default": [],
    }),
    strip_include_prefix = "src",
    visibility = ["//visibility:public"],
)

cc_binary(
    name = "bin/lua",
    srcs = ["src/lua.c"],
    visibility = ["//visibility:public"],
    deps = [":lua"],
)

cc_binary(
    name = "bin/luac",
    srcs = ["src/luac.c"],
    visibility = ["//visibility:public"],
    deps = [":lua"],
)

alias(
    name = "liblua",
    actual = ":lua",
    visibility = ["//visibility:public"],
)

load("@rules_cc//cc:cc_library.bzl", "cc_library")

DEFINES = select({
    "@platforms//os:windows": [
        "SC_BUILD",
        "SC_INCLUDE_FX",
        "_CRT_SECURE_NO_WARNINGS",
    ],
    "//conditions:default": [
        "SC_BUILD",
        "SC_INCLUDE_FX",
    ],
})

cc_library(
    name = "systemc_asm",
    srcs = select({
        "@platforms//cpu:aarch64": ["src/sysc/packages/qt/md/aarch64.s"],
        "@platforms//cpu:x86_64": ["src/sysc/packages/qt/md/iX86_64.s"],
        "//conditions:default": [],
    }),
    hdrs = select({
        "@platforms//cpu:aarch64": ["src/sysc/packages/qt/md/aarch64.h"],
        "@platforms//cpu:x86_64": ["src/sysc/packages/qt/md/iX86_64.h"],
        "//conditions:default": [],
    }),
    copts = select({
        "@platforms//os:linux": [
            "-x",
            "assembler-with-cpp",  # Enable preprocessing for .s files on Linux
        ],
        "@rules_cc//cc/compiler:msvc-cl": [],
        "//conditions:default": [],
    }),
    includes = ["src/sysc/packages/qt/md"],
    local_defines = DEFINES,
)

PLATFORM_COPTS = select({
    "@rules_cc//cc/compiler:msvc-cl": [
        "/wd4996",
    ],
    "//conditions:default": [
        "-Wno-deprecated",
    ],
})

PLATFORM_CXXOPTS = PLATFORM_COPTS + select({
    "@rules_cc//cc/compiler:msvc-cl": [
        "/std:c++17",
    ],
    "//conditions:default": [
        "-std=c++17",
    ],
})

cc_library(
    name = "systemc",
    srcs = glob(
        ["src/**/*.cpp"],
        exclude = ["src/sysc/packages/qt/**"],
    ) + [
        "src/sysc/packages/qt/qt.c",
    ],
    hdrs = glob([
        "src/**/*.h",
    ]) + [
        "src/systemc",
        "src/tlm",
    ],
    copts = PLATFORM_COPTS,
    cxxopts = PLATFORM_CXXOPTS,
    includes = ["src"],
    local_defines = DEFINES,
    visibility = ["//visibility:public"],
    deps = [":systemc_asm"],
)
